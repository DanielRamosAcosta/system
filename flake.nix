{
  description = "Configuración de NixOS de Daniel Ramos";

  inputs.nixpkgs.url = "github:NixOS/nixpkgs/nixos-25.11";

  inputs.disko.url = "github:nix-community/disko";
  inputs.disko.inputs.nixpkgs.follows = "nixpkgs";

  inputs.agenix.url = "github:ryantm/agenix";
  inputs.agenix.inputs.nixpkgs.follows = "nixpkgs";

  inputs.stylix.url = "github:nix-community/stylix/release-25.11";
  inputs.stylix.inputs.nixpkgs.follows = "nixpkgs";

  inputs.home-manager.url = "github:nix-community/home-manager/release-25.11";
  inputs.home-manager.inputs.nixpkgs.follows = "nixpkgs";

  inputs.nix-darwin.url = "github:nix-darwin/nix-darwin/nix-darwin-25.11";
  inputs.nix-darwin.inputs.nixpkgs.follows = "nixpkgs";

  inputs.nixpkgs-unstable.url = "github:NixOS/nixpkgs/nixos-unstable";

  inputs.jovian.url = "github:Jovian-Experiments/Jovian-NixOS";
  inputs.jovian.inputs.nixpkgs.follows = "nixpkgs-unstable";

  inputs.lanzaboote.url = "github:nix-community/lanzaboote/v1.1.0";
  inputs.lanzaboote.inputs.nixpkgs.follows = "nixpkgs-unstable";

  inputs.nixpkgs-node26.url = "github:NixOS/nixpkgs/4df1b885";

  outputs = {
    nixpkgs,
    disko,
    agenix,
    stylix,
    home-manager,
    nix-darwin,
    nixpkgs-node26,
    nixpkgs-unstable,
    jovian,
    lanzaboote,
    ...
  }:
    let
      nasSystem = "x86_64-linux";
      nasPkgs = nixpkgs.legacyPackages.${nasSystem};
      forAllSystems = nixpkgs.lib.genAttrs [ "aarch64-linux" "x86_64-linux" "aarch64-darwin" ];
    in {
      packages.${nasSystem} = {
        quadro-ctl = nasPkgs.callPackage ./packages/quadro-ctl.nix {};
      };

      devShells = forAllSystems (system:
        let pkgs = nixpkgs.legacyPackages.${system};
        in {
          default = pkgs.mkShell {
            packages = with pkgs; [
              nixos-rebuild-ng
              agenix.packages.${system}.default
            ];

            shellHook = ''
              export EDITOR=nano
            '';
          };
        }
      );

      nixosConfigurations = {
        nas = nixpkgs.lib.nixosSystem {
          system = nasSystem;
          modules = [
            ./hosts/nas
            disko.nixosModules.disko
            agenix.nixosModules.default
            {
              nixpkgs.overlays = [
                (_final: _prev: {
                  quadro-ctl = nasPkgs.callPackage ./packages/quadro-ctl.nix {};
                })
              ];
            }
          ];
        };

        workhorse = nixpkgs-unstable.lib.nixosSystem {
          system = "x86_64-linux";
          modules = [
            ./hosts/workhorse
            disko.nixosModules.disko
            agenix.nixosModules.default
            jovian.nixosModules.default
            lanzaboote.nixosModules.lanzaboote
          ];
        };

        siemens = nixpkgs.lib.nixosSystem {
          system = "x86_64-linux";
          modules = [
            ./hosts/siemens
            disko.nixosModules.disko
            agenix.nixosModules.default
            stylix.nixosModules.stylix
            home-manager.nixosModules.home-manager
            {
              home-manager.useGlobalPkgs = true;
              home-manager.useUserPackages = true;
              home-manager.users.dani = import ./hosts/siemens/home.nix;
            }
          ];
        };

        iso = nixpkgs.lib.nixosSystem {
          system = nasSystem;
          modules = [
            ./hosts/iso
            "${nixpkgs}/nixos/modules/installer/cd-dvd/installation-cd-minimal.nix"
          ];
        };
      };

      darwinConfigurations = {
        macbook = nix-darwin.lib.darwinSystem {
          system = "aarch64-darwin";
          modules = [
            ./hosts/macbook
            home-manager.darwinModules.home-manager
            {
              nixpkgs.overlays = [
                (_final: prev: {
                  nodejs_26 = nixpkgs-node26.legacyPackages.${prev.system}.nodejs_26;
                })
              ];
              home-manager.useGlobalPkgs = true;
              home-manager.useUserPackages = true;
              home-manager.users.danielramos.imports = [
                ./hosts/macbook/home
                agenix.homeManagerModules.default
              ];
            }
          ];
        };
      };
    };
}
